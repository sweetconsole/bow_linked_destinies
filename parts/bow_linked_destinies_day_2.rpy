
label bow_linked_destinies_day_2:
    stop ambience
    stop music
    stop sound
    window hide
    show blink
    $ renpy.pause(1.0, hard=True)
    $ backdrop = "days"
    $ new_chapter(2, u"Бантик связавший наши судьбы.\nДень первый")
    $ persistent.sprite_time = "day"
    $ day_time()

    scene black

    "Спал я как убитый, хотя почему как. Вчера действительно произошло много событий. Что будет сегодня? Сказать наверняка сложно."

    play ambience ambience_int_cabin_day loop fadein 3.0
    show bg int_house_of_mt_day 
    show unblink
    $ renpy.pause(1, hard=True)

    "Открыв глаза и посмотрев на будильник он показывал 6 утра. Ну, кто рано встаёт тому Бог подаёт. Посмотрев на соседнюю койку Ольги Дмитриевны там не оказалось, что показалось мне странным."
    
    play sound sfx_open_door_1
    $ renpy.pause(0.25, hard=True)
    show mt pioneer normal with dissolve
    
    "Я стал одеваться и в этот же момент дверь распахнулась, а в домик вошла радостная Ольга."

    show mt pioneer smile with dspr

    mt "О, ты уже проснулся. Я только хотела тебя будить."
    iv "А не рано ли? Не то чтобы я не выспался, просто подъём должен быть в часов 7 или 8?"

    show mt pioneer normal with dspr

    mt "7:30, в 8 утра линейка, после завтра и так далее по плану."
    "Снова эти планы. Всё идёт по плану, не так ли? Один лишь дедушка Ленин хороший был вождь."
    mt "Раз уж пораньше встал, то у тебя есть свободное время. Но сперва тебе не помешает сходить умыться. Умывальники знаешь где?"
    iv "Нет, буду рад, если расскажете."
    mt "Выйдешь из домика, повернёшь направо и пройдя чуть прямо снова направо. Так и дойдёшь."
    iv "А мыльно-рыльное полагается пионеру?"
    
    show mt pioneer smile with dspr

    mt "Конечно, всё для тебя."
    "Она протянула мне пакетик с зубным порошком и щёткой."
    iv "Ну, я пошёл."
    mt "Смотри не потеряйся, а то Славя мне рассказывала, как ты не дошёл один раз. Жду на линейке."

    play sound sfx_open_door_1
    play ambience ambience_camp_center_day loop fadein 2.0
    scene bg ext_house_of_mt_day with fade

    "Путь до умывальников было запомнить проще чем до домика Ольги."

    scene bg ext_washstand_day with fade

    "И вот я здесь, как тут вода включается?"
    
    play sound sfx_water_sink_stream loop fadein 2.0
    scene bg ext_washstand2_day with dissolve
    
    "Повернув довольно ржавый кран из него полилась арктическая вода, которая позволила моментально потерять остатки утренней сонливость. Вода обжигала, но не смотря на это мне всё-таки удалось умыться и почистить зубы."

    play ambience ambience_camp_center_day loop fadein 2.0
    scene bg ext_house_of_mt_day with fade

    "Не став долго задерживаться я сразу же пошёл до домика."

    show mt pioneer normal with dissolve

    "Оставив там всё лишнее, на пороге меня встретила Ольга Дмитриевна."

    show mt pioneer angry with dspr

    mt "Иван, куда собрался? Ты не забыл про линейку?"
    iv "Ну конечно, как же я мог забыть. Обязательно приду."

    show mt pioneer normal with dspr

    mt "Тогда идём, она уже скоро начнётся."
    "По правде говоря, идти совершенно не хотелось. Но что мне остаётся? От неё не спрятаться. Кажется, будто она найдёт и из под земли достанет. Если этого потребуют обстоятельства конечно."
    "Отбросив лишние мысли я последовал за Ольгой."

    scene bg ext_square_day with dissolve

    "И вот мы стоим на площади, уже потихоньку начинают собираться и другие отряды."

    scene cg d7_pioneers_leaving_without_us with fade

    "Простояв так ещё пару минут Ольга посчитала всех по головам и начала говорить. Я честно говоря её не слушал, мне не то чтобы было неинтересно, скорее просто не хотелось. Я летал в облаках и считал ворон. Они казались такими далёкими, что если была бы возможность дотянуться до них? О чём это я? Поди перегрелся на солнце."

    scene bg ext_square_day with dissolve
    
    show sl pioneer normal:
        xpos -0.5
        linear 2 xpos 0.05

    show mt pioneer normal:
        xpos 1
        linear 2 xpos 0.5
    
    "Вновь мои мечтания были прерваны Славей и Ольгой Дмитриевной, которые смотрели на меня и что-то хотели."
    iv "Что такое? У меня на лице что-то?"
    sl "Да нет, что ты просто обходной тебе выдать нужно."
    "Совсем забыл, меня ведь предупреждали об обходном."
    mt "Тебе нужно пройти места, которые находятся в списке. Получить подпись от ребят, которые заведуют там. И как закончишь, принеси мне. Обязательно запишись куда-нибудь."
    sl "Всё понял?"
    iv "Всё понял. Закончу и принесу всё в лучшем виде."

    scene bg ext_library_day with fade

    "Первым в списке оказалась библиотека. Её найти было не сложно, внешний вид выдавал её среди обычных домиков пионеров. Здание с большими окнами, утопающее в зелени рядом с которым была неизвестная пристройка."
    "Что ж, наверное самое интересное меня ждёт в музыкальном клубе. Может и записаться получится. Я как никак на гитаре играть умею и в целом люблю музыку. Быстрее начнём быстрее закончим. С этими мыслями я вошёл внутрь."

    play sound sfx_open_door_2
    play ambience ambience_library_day
    scene bg int_library_day with fade

    "Войдя в библиотеку первым делом я попробовал вдохнуть полной грудью. Я закашлялся, видимо тут давно не проветривали. Да и пыль с полок не вытирали. Передо мной стояло три стилажа заполненными книгами."

    scene cg bld_library_zhenya_asleep with dissolve

    "И где же библиотекарь? Но долго искать не пришлось, ведь он мирно посапывал облокотившись на стол обеими руками. Вернее она. Тут вообще можно встретить мужских особей? Или я попал в гарем? Внешний вид девушки показался мне забавным. Круглые очки, волос на голове скрючился в знак вопроса. Черты лица приятные."
    "Как бы мне не хотелось её будить, но мне нужно было получить её закорючку в свой обходной. Поэтому я легонько потряс её за плечо. Девушка хоть и не сразу, но всё-таки подала признаки жизни. Недовольным, но ещё сонным голосом она сказала."

    scene bg int_library_day
    show mz pioneer angry
    with dissolve

    mzp "Кто такой? Вам тут всем мёдом намазано что-ли? Чего тебе?"
    iv "Я Ваня, вчера приехал. Вот сейчас обходной нужно заполнить. Тут бы генеральную уборку провести не помешает. Да проветрить хорошенько, лишним не будет."

    show mz pioneer normal

    mz "Женя, очень приятно. Давай свой обходной. И так без тебя знаю, нашёлся умник."
    "Вырвав его у меня из рук, она мигом подписала его и вернув мне послала куда подальше. Мол ей ещё нужно важными делами заняться. Не став с ней спорить я покинул библиотеку."
    
    play sound sfx_open_door_2
    play ambience ambience_library_day
    scene bg ext_library_day with fade

    "Перед уходом стало интересно, что по мнению Жени является «важными делами».Заглянув в окно я увидел схожую картину, которую видел при входе в библиотеку. Она снова мирно посапывает. Оно и неудивительно, кому будет интересно в такую жару сидеть в духоте и читать тонну литературу про Коммунизм, сборники сочинений Ленина и Карла Маркса."
    # Дальше я решил пойти в «Клубы». Подойдя ближе к зданию клубов я смог рассмотреть детали, которые не увидел в первый раз. Доска объявлений, на которой красивым почерком было написано расписание . Различные надписи на стенах.
    # Постучавшись и немного подождав я вошёл. Внутри же царил полный хаос. Различные мелкие детали разбросанные повсюду, разобранная техника, множество коробок с барахлом. По всему зданию стоял шум, кипела работа. Моё внимание привлекла ничем не примечательная длинная верёвка. Через мгновенье из каморки вышел пионер в очках. Его осанка была ровный, волосы аккуратно уложены. 
    # Пионер: Привет, ты ведь новенький, верно? 
    # Ваня: Да, новенький. Вчера приехал и вот пришёл подписать обходной.
    # Пионер: Меня Саша зовут, можно просто Шурик. 
    # Ваня: Очень приятно, я Ваня.
    # Шурик: Обходной тебе подпишет Серёжа, мне сейчас ни до этого. Нужно разобрать старое радио и достать оттуда микросхемы
    # После этих слов из каморки вышел ещё один пионер.


    